WITH DuplicateUsernames AS (
    SELECT 
        ISNULL(si.ItemValue, N'No Username field') AS [Username]
    FROM
        tbSecret s
    JOIN
        tbSecretField sf WITH (NOLOCK)
        ON s.SecretTypeID = sf.SecretTypeID
    JOIN
        tbSecretItem si WITH (NOLOCK)
        ON sf.SecretFieldID = si.SecretFieldID
        AND s.SecretID = si.SecretID
    WHERE 
        sf.SecretFieldName = 'Username'
        AND s.Active = 1
    GROUP BY 
        ISNULL(si.ItemValue, N'No Username field')
    HAVING 
        COUNT(*) > 1
)

SELECT
    s.SecretName AS [Secret Name],
    ISNULL(si.ItemValue, N'No Username field') AS [Username],
    ISNULL(fp.FolderPath, N'No folder assigned') AS [Folder Path],
    s.SecretID,
    s.LastHeartBeatCheck,
    CASE
        WHEN s.LastHeartBeatStatus = 0 THEN 'Failed'
        WHEN s.LastHeartBeatStatus = 4 THEN 'UnableToConnect'
        WHEN s.LastHeartBeatStatus = 5 THEN 'UnknownError'
        WHEN s.LastHeartBeatStatus = 6 THEN 'IncompatibleHost'
        WHEN s.LastHeartBeatStatus = 7 THEN 'AccountLockedOut'
        WHEN s.LastHeartBeatStatus = 8 THEN 'DnsMismatch'
        WHEN s.LastHeartBeatStatus = 9 THEN 'UnableToValidateServerPublicKey'
        WHEN s.LastHeartBeatStatus = 10 THEN 'Processing'
        WHEN s.LastHeartBeatStatus = 11 THEN 'ArgumentError'
        WHEN s.LastHeartBeatStatus = 12 THEN 'AccessDenied'
        ELSE 'Failed Unknown'
    END AS 'HeartbeatStatus'
FROM
    tbSecret s
JOIN
    tbSecretField sf WITH (NOLOCK)
    ON s.SecretTypeID = sf.SecretTypeID
JOIN
    tbSecretItem si WITH (NOLOCK)
    ON sf.SecretFieldID = si.SecretFieldID
    AND s.SecretID = si.SecretID
LEFT JOIN
    vFolderPath fp WITH (NOLOCK)
    ON s.FolderId = fp.FolderId
JOIN 
    DuplicateUsernames du
    ON ISNULL(si.ItemValue, N'No Username field') = du.Username
WHERE 
    sf.SecretFieldName = 'Username'
    AND s.Active = 1
ORDER BY 
    s.SecretID ASC;
